plugins{
    id 'library.deps-conventions'
    id 'library.java-conventions'

    // The following plugin is used to build the OSGI bundle
    id 'biz.aQute.bnd.builder' version '6.4.0'
}

repositories {
    mavenLocal()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

sourceSets {
    src {
        java {
            srcDirs "src/main/java/"
        }
    }
}

dependencies {
    implementation project(':wrims-core')

    implementation libs.commons.io
    implementation libs.jfreechart
    implementation libs.hec.monolith
    implementation libs.hec.nucleus.metadata

    implementation fileTree(dir: 'libs/eclipse', include: ['*.jar'])
    implementation fileTree(dir: 'libs/x64', include: ['*.jar'])
    implementation fileTree(dir: 'libs/x64', include: ['*.dll'])

    // The following dependencies are used to build the OSGI bundle
    implementation 'org.osgi:org.osgi.core:6.0.0'
    implementation 'org.osgi:org.osgi.compendium:5.0.0'
}

configurations.configureEach {
    exclude group: "javax.media", module: "jai_core"
    exclude group: "javax.media", module: "jai_codec"
    exclude group: "com.sun.media", module: "jai_imageio"
}

tasks.register('copyDependencies', Copy) {
    from(configurations.runtimeClasspath) {
        exclude '**/org.eclipse.*.jar'
    }
    into 'build/tmp/libs'
}

compileJava.dependsOn(copyDependencies)
