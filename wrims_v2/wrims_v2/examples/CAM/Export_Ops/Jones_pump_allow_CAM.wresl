/*************************************
Jones_pump_allow_CAM.wresl
 
Hao Xie and Nazrul Islam DWR (02/08/2012)

There is no regulation for Jones at this time for CAM. However, we just mimic Banks PP. 
Defines Standard allowable & Max allowable pumping at Jones.  Jones can increase pumping
up to 1/3 of San Joaquin River flow at Vernalis if flow at Vernalis is greater than 1000 cfs.
    If SJR flow @ Vernalis > 1000 cfs then,
    Jones_max_allow = Jones_standard_allow + 1/3*(SJR @ Vernalis)
    But only upto physical capacity.
**************************************/



! Number of days in which the Jones limit *IS* increased by 1/3 of SJR flow
define(CAMmonths) JonesDaysIncr {
       select   DaysIncr
       from     JonesLimits
       where    month=mv($m)
}

! Number of days in which the Jones limit *IS NOT* increased by 1/3 of SJR flow
define(CAMmonths) JonesDaysNoIncr { value di($m) - JonesDaysIncr($m)}

! Finds the Max Pump Capacity with the allowable increase
define(CAMmonths) JonesMax1 {
       case NoIncrease {
            condition always
            value     JonesCapPermit($m) }
}

! Limit Pumping to allowable, weight for days in which increase is allowed
define(CAMmonths) JonesMaxAllow {value JonesMax1($m)*JonesDaysIncr($m)/di($m) + JonesCapPermit($m)*JonesDaysNoIncr($m)/di($m) }

! Limit Pumping to Allowable and Physical Capacity, whichever is less
goal(CAMmonths) JonesLessThanAllow { D_Jones($m) < JonesMaxAllow($m)}
goal(CAMmonths) JonesLessThanPhys  { D_Jones($m) < JonesCapPhys($m)}

! Output Allowable Pumping Cap for Checking Only
define(CAMmonths) JonesAllowOut {alias JonesMaxAllow($m) kind 'EXPORT-CTRL-JONES' UNITS 'CFS' }

define int_Shsta1500{INTEGER std kind 'INTEGER' UNITS 'NONE'} 
goal(CAMmonths) compareShsta1500{(int_Shsta1500($m)-0.5)*(S_Shsta($m-1)-1500)>=0}

! Jones PP Minimum Cap
define(CAMmonths) Jonesminpump {
    value 600+int_Shsta1500($m)*200.
}
define(CAMmonths) JonesMinPumpDV {alias JonesMinPump($m) kind 'EXPORT-CTRL-JONES' units 'cfs'}

define(CAMmonths) int_Shsta2000{INTEGER std kind 'INTEGER' UNITS 'NONE'} 
goal(CAMmonths) compareShsta2000{(int_Shsta2000($m)-0.5)*(S_Shsta($m-1)-2000)>=0}

define(CAMmonths) int_Folsm400{INTEGER std kind 'INTEGER' UNITS 'NONE'} 
goal(CAMmonths) compareFolsm400{(int_Folsm400($m)-0.5)*(S_Folsm($m-1)-400)>=0}

goal(CAMmonths) setJonesminpump {
    lhs D_Jones_cvp($m)
    case Joneslowshasta {
        condition always
    	rhs Jonesminpump($m)
        lhs>rhs penalty (65*int_Shsta2000($m)+(1-int_Shsta2000($m))*65*int_Shsta1500($m)*int_Folsm400($m))*taf_cfs($m) + 1
        lhs<rhs penalty 2700 }
}
